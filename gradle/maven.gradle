/*
 * Copyright 2010-2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

project.poms = [project.install.repositories.mavenInstaller.pom]
uploadArchives {
    project.deployer = repositories.mavenDeployer {
        def username = project.properties["codehausUsername"] ?: ''
        def password = project.properties["codehausPassword"] ?: ''

        uploadDescriptor = true
        repository(url: codehausReleaseUrl) {
            authentication(userName: username, password: password)
        }
        snapshotRepository(url: codehausSnapshotUrl) {
            uniqueVersion = true
            authentication(userName: username, password: password)
        }
    }
    project.poms << project.deployer.pom
}

project.pomModifications = []
project.modifyPom = { Closure modification -> project.pomModifications << modification }
project.poms*.whenConfigured { pom ->
    project.pomModifications.each { modification ->
        pom.project {
            modification.delegate = delegate
            modification.resolveStrategy = Closure.DELEGATE_FIRST
            modification()
        }
    }
}

project.modifyPom {
    name "Griffon Launcher"
    description project.description
    url "http://griffon.codehaus.org"
    inceptionYear "2011"
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url "https://github.com/griffon/griffon-launcher/"
    }
    developers {
        [aalmiray: "Andres Almiray"].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role "Developer"
                }
            }
        }
    }
}
